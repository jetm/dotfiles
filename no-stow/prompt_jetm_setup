#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Javier Tiá <javier.tia@gmail.com>
#
# Screenshots:
#   @TODO: add image prompt URL
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies
pmodload 'helper'

function prompt_jetm_git_info {
  # We can safely split on ':' because it isn't allowed in ref names.
  IFS=':' read _git_target _git_post_target <<<"$3"

  # The target actually contains 3 space separated possibilities, so we need to
  # make sure we grab the first one.
  _git_target=$(coalesce ${(@)${(z)_git_target}})

  if [[ -z "$_git_target" ]]; then
    # No git target detected, flush the git fragment and redisplay the prompt.
    if [[ -n "$_prompt_jetm_git" ]]; then
      _prompt_jetm_git=''
      zle && zle reset-prompt
    fi
  else
        # Default values for the appearance of the prompt. Configure at will
    dotfiles_br=$(git -C ${HOME}/repos/dotfiles symbolic-ref --short HEAD)

    if [[ "${dotfiles_br}" == "master" ]]; then
      WHERE_I_AM="H"
    else
      WHERE_I_AM="W"
    fi

    # Git target detected, update the git fragment and redisplay the prompt.
    _prompt_jetm_git="${_git_target}${_git_post_target}"
    zle && zle reset-prompt
  fi
}

function prompt_jetm_async_git {
  cd -q "$1"
  if (( $+functions[git-info] )); then
    git-info
    print ${git_info[status]}
  fi
}

function prompt_jetm_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_jetm_pwd=$(prompt-pwd)

  # Kill the old process of slow commands if it is still running.
  async_flush_jobs async_jetm_git

  # Handle updating git data. We also clear the git prompt data if we're in a
  # different git root now.
  if (( $+functions[git-dir] )); then
    local new_git_root="$(git-dir 2> /dev/null)"
    if [[ $new_git_root != $_jetm_cur_git_root ]]; then
      _prompt_jetm_git=''
      _jetm_cur_git_root=$new_git_root
    fi
  fi

  # Compute slow commands in the background.
  async_job async_jetm_git prompt_jetm_async_git "$PWD"
}

function prompt_jetm_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)
  _prompt_jetm_precmd_async_pid=0
  _prompt_jetm_precmd_async_data=$(mktemp "${TMPDIR:-/tmp}/jetm-prompt-async-XXXXXXXXXX")

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz async && async

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_jetm_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{2}❮%F{3}❮%F{1}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}↑%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}↓%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}d%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}m%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}s%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}…%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '%b %p %c:%s%A%B%S%a%d%m%r%U%u'

  # Get the async worker set up
  async_start_worker async_jetm_git -n
  async_register_callback async_jetm_git prompt_jetm_git_info
  _jetm_cur_git_root=''

  _prompt_jetm_git=''
  _prompt_jetm_pwd=''

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{9}%n%f%F{7}@%f%F{3}%m%f "}%F{4}${_prompt_jetm_pwd}%f ${WHERE_I_AM} ${_prompt_jetm_git}
%(!. %B%F{1}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{1}⏎%f)${VIM:+" %B%F{6}V%f%b"}'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_jetm_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'jetm'
}

prompt_jetm_setup "$@"
